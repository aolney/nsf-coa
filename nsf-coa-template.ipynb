{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Generate NSF Collaborators and Other Affiliations Information from Your Publications\n",
    "\n",
    "[NSF grant proposal guidelines](https://www.nsf.gov/pubs/policydocs/pappg18_1/pappg_2.jsp) require the listing of all possible reviewers who may have a conflict of interest with reviewing your application.\n",
    "\n",
    "Some of these reviewers are relatively static (your Ph.D. advisor), but the bulk of them are people you've published with in the last 48 months. This notebook helps specifically with creating that section of the [COA form](https://www.nsf.gov/bfa/dias/policy/coa.jsp).\n",
    "\n",
    "To start, you will either need:\n",
    "\n",
    "1. A citation database of your publications\n",
    "\n",
    "2. A Google Scholar profile ([see how to create](https://libguides.reading.ac.uk/boost/google-scholar-profile))\n",
    "\n",
    "The output of the notebook will be tab-delimited data that can be copy/pasted into the NSF template\n",
    "\n",
    "## Step 1: Get Citations in RIS format\n",
    "\n",
    "### You have a citation database of your publications\n",
    "\n",
    "These instructions are generic because there are many different types of citation database. The logical steps are:\n",
    "\n",
    "1. Do a query to select all  publications that you are either an author of OR an editor of. In JabRef this would be, e.g., `author=olney or editor=olney`\n",
    "\n",
    "2. Select the publications matching your query and then save/export in [RIS format](https://en.wikipedia.org/wiki/RIS_%28file_format%29). In JabRef this would be `File -> Export selected entries -> Save dialogue: enter file name and choose RIS file type`\n",
    "\n",
    "### You have a Google Scholar profile\n",
    "\n",
    "Note Google Scholar does not have gold-standard citation information but it is very likely to be correct for authors IF you have done some curation. It is not unusual to have to purge publications that aren't yours on initial set up.\n",
    "\n",
    "1. Install [Publish or Perish](https://harzing.com/resources/publish-or-perish)\n",
    "\n",
    "2. Do `Menu -> Query -> New Google Scholar Query -> input your name` followed by `Query pane -> Right click your name -> Save to File -> Save to RIS`\n",
    "\n",
    "## Step 2: Change the information below and run"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "DONE - CHECK YOUR OUTPUT FOLDER\n"
     ]
    }
   ],
   "source": [
    "//CHANGE THIS TO YOUR FILE PATH!\n",
    "let filePath = \"\"\"/z/aolney/grant_proposals/1-CV/nsf-coauthor/092520.ris\"\"\"\n",
    "\n",
    "//CHANGE THIS TO YOUR LAST NAME!\n",
    "let yourName = \"Olney\"\n",
    "\n",
    "//OPTIONAL - IF YOU HAVE AN OLD NSF COA FORM WITH INSTITUTIONAL INFORMATION, THEN\n",
    "// 1. SAVE AS TAB DELIMITED TEXT\n",
    "// 2. PUT THE PATH BELOW\n",
    "// OTHERWISE LEAVE EMPTY, E.G. \"\"\n",
    "let oldPath = \"\" //\"\"\"/z/aolney/grant_proposals/1-CV/nsf-coauthor/coa-olney-datawhys-120819.csv\"\"\"\n",
    "\n",
    "open System.IO\n",
    "open System.Text.RegularExpressions\n",
    "\n",
    "type RISElement =\n",
    "    {\n",
    "        Tag : string\n",
    "        Content : string\n",
    "    }\n",
    "\n",
    "//remove initials and trim\n",
    "let removeInitial name =\n",
    "    Regex.Replace( name, @\"[A-Z]\\.\",\"\").Trim()\n",
    "    \n",
    "//Map of old names to institutions. Assume relevant rows start with [A-Z]:\n",
    "let institutionMap =\n",
    "    if oldPath <> \"\" then\n",
    "        oldPath\n",
    "        |> System.IO.File.ReadAllLines\n",
    "        |> Seq.choose( fun row ->\n",
    "            let s = row.Split('\\t')\n",
    "            if s.Length > 2 && s.[0].Trim().EndsWith(\":\") then\n",
    "                let collaborator = s.[1] |> removeInitial                    \n",
    "                let institution = s.[2].Trim()\n",
    "                if collaborator <> \"\" && institution <> \"\" then\n",
    "                    Some(collaborator, institution)\n",
    "                else \n",
    "                    None\n",
    "            else\n",
    "                None\n",
    "        )\n",
    "        |> Map.ofSeq\n",
    "    else\n",
    "        Map.empty\n",
    "    \n",
    "let getEntries (entryText : string) =\n",
    "    entryText.Split('\\n')\n",
    "    //remove rows without elements\n",
    "    |> Seq.filter( fun row -> row.Contains(\"-\"))\n",
    "    //map row to RISElement\n",
    "    |> Seq.map( fun element -> \n",
    "        let delimIndex = element.IndexOf(\"-\")\n",
    "        let tag = element.Substring(0, delimIndex).Trim()\n",
    "        let content = element.Substring( delimIndex + 1 ).Trim() |> removeInitial\n",
    "        { Tag = tag; Content = content}\n",
    "    )\n",
    "    \n",
    "let authorTuples,editorTuples =\n",
    "    //ER is end of record; use to group\n",
    "    Regex.Split(\n",
    "        filePath |> File.ReadAllText,\n",
    "        @\"^ER\\s*-\\s*\",\n",
    "        RegexOptions.Multiline\n",
    "        )\n",
    "    //Remove blank lines\n",
    "    |> Seq.filter( fun entryText -> entryText <> \"\" )\n",
    "    //Map entry rows to authors and editors in the last 4 years \n",
    "    |> Seq.collect( fun entryText ->\n",
    "        let entries = entryText |> getEntries\n",
    "        let year = \n",
    "            match entries |> Seq.tryFind( fun entry -> entry.Tag = \"Y1\" ) with\n",
    "            | Some(y) -> y.Content.Substring(0,4) //year not month\n",
    "            | None -> \"NA\"\n",
    "            \n",
    "        //We assume authors are always AU\n",
    "        let authors = entries |> Seq.choose( fun entry -> if entry.Tag = \"AU\" then Some(entry) else None )\n",
    "        //Tricky: RIS doesn't handle editors super well, e.g. Jabref makes them A2\n",
    "        let editors = entries |> Seq.choose( fun entry -> if entry.Tag = \"A2\" || entry.Tag = \"ED\" then Some(entry) else None)\n",
    "        let youEditor = editors |> Seq.exists(  fun entry -> entry.Content.Contains(yourName))\n",
    "       \n",
    "        let temp = new ResizeArray<string*string*string>()\n",
    "\n",
    "        for author in authors do\n",
    "            temp.Add( author.Content, year, \"A:\")\n",
    "        \n",
    "        //They are coeditors if you are among them\n",
    "        if youEditor then\n",
    "            for editor in editors do\n",
    "                temp.Add( editor.Content, year, \"E:\")\n",
    "            \n",
    "        //collect flattens these\n",
    "        temp\n",
    "        )\n",
    "    //Filter out only last 48 months (~ 4 years)\n",
    "    |> Seq.filter( fun (c,y,t) -> \n",
    "        match y |> System.Int32.TryParse with\n",
    "        | true, v -> System.DateTime.Now.Year - v < 4\n",
    "        | false, _ -> true //keep all NAs for user to fix manually\n",
    "    )\n",
    "    |> Seq.toList\n",
    "    //Partition to separate authors and editors so we can have identical names on both\n",
    "    |> List.partition (fun (c,y,t) -> t = \"A:\" )\n",
    "   \n",
    "//Get only the last year of collaboration for each individual\n",
    "let getLastActive tuples =\n",
    "    tuples\n",
    "    |> List.groupBy (fun (c,y,t) -> c )\n",
    "    |> List.map( fun (g,tuples) -> tuples |> Seq.maxBy( fun(c,y,t) -> y) ) //works on strings b/c 4 digit year\n",
    "    |> List.sortBy( fun (c,y,t) -> t + c) \n",
    "\n",
    "//map to something printable\n",
    "let output = \n",
    "    //Get last active year for authors and editors and concatenate\n",
    "    (authorTuples |> getLastActive) @ (editorTuples |> getLastActive)\n",
    "    |> Seq.map( fun (c,y,t) -> \n",
    "        let institution = \n",
    "            match institutionMap.TryFind c with\n",
    "            | Some inst -> inst\n",
    "            //Will create a hyperlink in excel to search the name in Google Scholar to get affiliation\n",
    "            | None -> \"\\\"=HYPERLINK(\\\"\\\"https://scholar.google.com/scholar?hl=en&q=\" + c + \"&btnG=\\\"\\\", \\\"\\\"SearchAffiliation\\\"\\\")\\\"\"\n",
    "            \n",
    "        t + \"\\t\" + c + \"\\t\" + institution + \"\\t\\t\" + y )\n",
    "\n",
    "//Done!\n",
    "System.IO.File.WriteAllLines( filePath + \"_OUTPUT.tsv\", output )\n",
    "\n",
    "printfn \"DONE - CHECK YOUR OUTPUT FOLDER\""
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Step 3: Copy/paste\n",
    "\n",
    "The output of the notebook will be tab-delimited data that can be copy/pasted into the NSF template.\n",
    "\n",
    "Things to look out for:\n",
    "\n",
    "- If you imported affiliations from a previous COA, double check any colleagues who have moved (esp. students).\n",
    "- Double check for duplicates. This happens if colleagues are not consistently using a professional name.\n",
    "- Add missing affiliations. `Ctrl+click` on the appropriate spreadsheet cell should launch a browser query to Google Scholar to make this easier.\n",
    "- Remember authors and editors go in different sections."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": ".NET (F#)",
   "language": "F#",
   "name": ".net-fsharp"
  },
  "language_info": {
   "file_extension": ".fs",
   "mimetype": "text/x-fsharp",
   "name": "C#",
   "pygments_lexer": "fsharp",
   "version": "4.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
